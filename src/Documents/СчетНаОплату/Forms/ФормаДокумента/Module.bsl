
&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	Товар = Элементы.Товары.ТекущиеДанные.Номенклатура;
	Элементы.Товары.ТекущиеДанные.Цена = ПолучитьЦену(Товар);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЦену(Товар)
	//Возврат Товар.ЦенаПродажи;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|	ВЫБРАТЬ
	|	ЦенаПродажи
	|ИЗ
	|	Справочник.Номенклатура
	|ГДЕ
	|	Ссылка = &АБВ";
	
	Запрос.УстановитьПараметр("АБВ", Товар); 
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.ЦенаПродажи;
	
КонецФункции

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	РассчитатьСумму(Элементы.Товары.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	РассчитатьСумму(Элементы.Товары.ТекущиеДанные);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСумму(Стр)
	Стр.Сумма = Стр.Цена * Стр.Количество;
КонецПроцедуры


&НаСервере
Процедура ДатьСкидкуНаСервере()
	Для каждого Стр Из Объект.Товары Цикл
		Стр.Цена = Стр.Цена * 0.9;
		РассчитатьСумму(Стр);
	КонецЦикла; 
КонецПроцедуры


&НаКлиенте
Процедура ДатьСкидку(Команда)
	ДатьСкидкуНаСервере();
КонецПроцедуры


&НаСервере
Функция Печать1НаСервере()
	ТабДок = Новый ТабличныйДокумент;
	Макет = Документы.СчетНаОплату.ПолучитьМакет("Макет");
	ОблШапка = Макет.ПолучитьОбласть("ОблШапка");
	ОблСтрока = Макет.ПолучитьОбласть("ОблСтрока");
	ОблПодвал = Макет.ПолучитьОбласть("ОблПодвал");

	ОблШапка.Параметры.Номер = Объект.Номер;
	ОблШапка.Параметры.Дата = Формат(Объект.Дата, "Л=sq_AL; ДЛФ=DD"); 
	ОблШапка.Параметры.Контрагент = Объект.Контрагент;
	ОблШапка.Параметры.Договор = Объект.Договор;
	
	ТабДок.Вывести(ОблШапка);	
	
	Для каждого Стр Из Объект.Товары Цикл
		ОблСтрока.Параметры.Заполнить(Стр);
		ТабДок.Вывести(ОблСтрока);
	КонецЦикла; 
	
	СуммаИтого = Объект.Товары.Итог("Сумма");
	ОблПодвал.Параметры.СуммаПрописью = ЧислоПрописью(СуммаИтого,,"рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2");
	
	ТабДок.ОтображатьГруппировки = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ОтображатьСетку = Ложь;
	
	ТабДок.Вывести(ОблПодвал);
	
	Возврат ТабДок;
КонецФункции


&НаКлиенте
Процедура Печать1(Команда)
	ТабДок = Печать1НаСервере();
	ТабДок.Показать();
КонецПроцедуры


&НаКлиенте
Процедура ВводНаОснованииВручную(Команда)
	
	Парам = Новый Структура;
	Парам.Вставить("Основание", Объект.Ссылка);
	Парам.Вставить("Скидка", 10);
	
	ОткрытьФорму("Документ.РасходнаяНакладная.Форма.ФормаДокумента", Парам);
	
КонецПроцедуры
 