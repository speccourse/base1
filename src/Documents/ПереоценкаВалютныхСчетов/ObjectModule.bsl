
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрБухгалтерииОстатки.Счет КАК Счет,
	|	РегистрБухгалтерииОстатки.Субконто1 КАК Субконто1,
	|	РегистрБухгалтерииОстатки.Субконто2 КАК Субконто2,
	|	РегистрБухгалтерииОстатки.Валюта КАК Валюта,
	|	РегистрБухгалтерииОстатки.ВалСуммаОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) - РегистрБухгалтерииОстатки.СуммаОстаток КАК Отклонение,
	|	РегистрБухгалтерииОстатки.Счет.Вид КАК СчетВид,
	|	РегистрБухгалтерииОстатки.Счет.ВидыСубконто.(
	|		НомерСтроки КАК СчетНомерСтроки,
	|		ВидСубконто КАК СчетВидСубконто
	|	) КАК ВидыСубконто
	|ИЗ
	|	РегистрБухгалтерии.РегистрБухгалтерии.Остатки(&МоментВремени, Счет.Валютный, , ) КАК РегистрБухгалтерииОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютСрезПоследних
	|		ПО РегистрБухгалтерииОстатки.Валюта = КурсыВалютСрезПоследних.Валюта
	|ГДЕ
	|	(ВЫРАЗИТЬ(РегистрБухгалтерииОстатки.ВалСуммаОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) - РегистрБухгалтерииОстатки.СуммаОстаток КАК ЧИСЛО(15, 2))) <> 0";
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени() );
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		                                 
		Проводка = Движения.РегистрБухгалтерии.Добавить();
		Проводка.Период = Дата;
		//Отклонение = Окр(Выборка.отклонение,2);
		Если (Выборка.Отклонение > 0 И Выборка.СчетВид <> ВидСчета.Пассивный) ИЛИ
			 (Выборка.Отклонение < 0 И Выборка.СчетВид = ВидСчета.Пассивный) Тогда
			//Хорошо!
			Проводка.СчетКт = ПланыСчетов.ПланСчетов.Прибыль; 
			Проводка.СчетДт = Выборка.Счет;
			Проводка.ВалютаДт = Выборка.Валюта;
			
			ВыборкаСубконто = Выборка.ВидыСубконто.Выбрать();
			Пока ВыборкаСубконто.Следующий() Цикл
				Проводка.СубконтоДт[ВыборкаСубконто.СчетВидСубконто]= Выборка["Субконто" + ВыборкаСубконто.СчетНомерСтроки] ; 
			КонецЦикла; 
						
		Иначе
			//Плохо!
			Проводка.СчетДт = ПланыСчетов.ПланСчетов.Прибыль;
			Проводка.СчетКт = Выборка.Счет;
			Проводка.ВалютаКт = Выборка.Валюта;
			ВыборкаСубконто = Выборка.ВидыСубконто.Выбрать();
			Пока ВыборкаСубконто.Следующий() Цикл
				Проводка.СубконтоКт[ВыборкаСубконто.СчетВидСубконто]= Выборка["Субконто" + ВыборкаСубконто.СчетНомерСтроки] ; 
			КонецЦикла; 
		КонецЕсли; 
		
		Проводка.Сумма = МАКС(Выборка.Отклонение, - Выборка.Отклонение);
		Проводка.СодержаниеПроводки = "Переоценка валюты";
		
	КонецЦикла; 
	Движения.РегистрБухгалтерии.Записывать = Истина;

КонецПроцедуры
